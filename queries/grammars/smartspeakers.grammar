# Context-free grammar for smartspeakers module

Query → QSpeaker '?'?
QSpeaker → QSpeakerQuery
QSpeakerQuery →
    QSpeakerResume
    | QSpeakerPause
    | QSpeakerNextSong
    | QSpeakerLastSong
    | QSpeakerPlayRadio
    | QSpeakerIncreaseVolume
    | QSpeakerDecreaseVolume
    # | QSpeakerPlayPlaylist
    # QSpeakerGera/nhbh QSpeakerMakeRest
    # | QSpeakerSetja/nhbh QSpeakerSetRest
    # | QSpeakerLáta/nhbh QSpeakerLetRest
    # | QSpeakerTurnOnOrOffVerb QSpeakerTurnOrOffOnRest
    # | QSpeakerPlayOrPauseVerb QSpeakerPlayOrPauseRest
    # | QSpeakerIncreaseOrDecreaseVerb QSpeakerIncreaseOrDecreaseRest
    # | QSpeakerSkippa/nhbh
    # | QSpeakerNewSetting/fall

# Common inflections of verbs at start of commands:
# - Infinitive, active voice (e.g. "gera")
# - Imperative (e.g. "gerðu")
/nhbh = gmnh bh

QSpeakerGera/nhbh → 'gera:so'/nhbh
QSpeakerSetja/nhbh → 'setja:so'/nhbh | 'stilla:so'/nhbh
QSpeakerBreyta/nhbh → 'breyta:so'/nhbh
QSpeakerLáta/nhbh → 'láta:so'/nhbh
QSpeakerKveikja/nhbh → 'kveikja:so'/nhbh
QSpeakerSlökkva/nhbh → 'slökkva:so'/nhbh
QSpeakerSpila/nhbh → 'spila:so'/nhbh
QSpeakerHækka/nhbh → 'hækka:so'/nhbh | 'auka:so'/nhbh
QSpeakerLækka/nhbh → 'lækka:so'/nhbh | 'minnka:so'/nhbh
QSpeakerPása/nhbh →
    'stöðva:so'/nhbh
    | 'stoppa:so'/nhbh
    | 'stofna:so'/nhbh # TODO: Fix in beautified query
    # "pásaðu" is not recognized by BÍN, but is common in casual speech
    | "pásaðu"
    | "pása"
    | 'gera:so'/nhbh "hlé" "á"
QSpeakerSkippa/nhbh → 'skippa:so'/nhbh | 'skipa:so'/nhbh
QSpeakerSkipta/nhbh → 'skipta:so'/nhbh | "skipt"
QSpeakerVeraVerða → "vera:so" | "verða:so"

# FIXME: "Stilltu aftur á tónlist í hátalarann"
QSpeakerResume →
    # "Kveiktu aftur á tónlist í stofunni"
    QSpeakerKveikja/nhbh "aftur"? "á"? QSpeakerTónlist_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Kveiktu á hátölurunum í stofunni"
    | QSpeakerKveikja/nhbh "aftur"? "á"? QSpeakerHátalari_þgf QSpeakerLoc?
    # "Spilaðu aftur tónlist í hátölurunum í stofunni"
    | QSpeakerSpila/nhbh "aftur"? "áfram"? QSpeakerTónlist_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Spilaðu í hátölurunum í stofunni"
    | QSpeakerSpila/nhbh QSpeakerÍHátalara? QSpeakerLoc?
    # "Stilltu á tónlist í stofunni"
    | QSpeakerSetja/nhbh "aftur"? "á"? QSpeakerTónlist_þf QSpeakerÍHátalara? QSpeakerLoc?

#FIXME: "Slökktu í stofunni" (lights or speakers?)
QSpeakerPause →
    # "Slökktu á tónlistinni í stofunni"
    QSpeakerSlökkva/nhbh "á"? QSpeakerTónlistOrÚtvarp_þgf QSpeakerÍHátalara? QSpeakerLoc?
    # "Slökktu tónlistina í stofunni"
    | QSpeakerSlökkva/nhbh QSpeakerTónlistOrÚtvarp_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Slökktu á hátölurunum"
    | QSpeakerSlökkva/nhbh QSpeakerÁOrÍ? QSpeakerHátalari_þgf QSpeakerLoc?
    # "Pásaðu tónlistina í eldhúsinu"
    | QSpeakerPása/nhbh QSpeakerTónlistOrÚtvarp_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Settu útvarpið á pásu"
    | QSpeakerSetja/nhbh QSpeakerTónlistOrÚtvarp_þf? "á"? "pásu"  QSpeakerÍHátalara? QSpeakerLoc?

QSpeakerNæstaLag → "næsta" "lag:hk"
QSpeakerYfirÍOrYfirÁ → "yfir" QSpeakerÁOrÍ
QSpeakerNextSong →
    QSpeakerSkippa/nhbh
    | QSpeakerNæstaLag
    | "ég" "vil" QSpeakerNæstaLag
    | QSpeakerSetja/nhbh "á"? QSpeakerNæstaLag
    | QSpeakerSkipta/nhbh QSpeakerYfirÍOrYfirÁ QSpeakerNæstaLag
    | QSpeakerSkippa/nhbh QSpeakerYfirÍOrYfirÁ QSpeakerNæstaLag

#FIXME: How do user's say this? "Spólaðu til baka" "Farðu aftur á bak?". Complicated.
QSpeakerLastSong → ""

QSpeakerPlayRadio →
    QSpeakerPlayRadioWithStation
    | QSpeakerPlayRadioNoStation

# FIXME: "Spilaðu Rondó" currently conflicts with Spotify
QSpeakerPlayRadioWithStation →
    # "Settu hátalarana yfir á útvarpsstöðina X í eldhúsinu"
    QSpeakerSetja/nhbh QSpeakerHátalari_þf? "yfir"? "á"? QSpeakerÚtvarpsstöðX_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Kveiktu á Rondó"
    | QSpeakerKveikja/nhbh "á" QSpeakerÚtvarpsstöðX_þgf QSpeakerÍHátalara? QSpeakerLoc?
    # Spilaðu FM957 í eldhúsinu
    | QSpeakerSpila/nhbh QSpeakerÚtvarpsstöðX_þf QSpeakerÍHátalara? QSpeakerLoc?

QSpeakerPlayRadioNoStation →
    # "Settu hátalarana yfir á útvarpið í stofunni"
    QSpeakerSetja/nhbh QSpeakerHátalari_þf? "yfir"? "á"? QSpeakerÚtvarp_þf QSpeakerÍHátalara? QSpeakerLoc?
    # "Spilaðu útvarpið í hátölurunum í stofunni"
    | QSpeakerSpila/nhbh "aftur"? "áfram"? QSpeakerÚtvarp_þf QSpeakerÍHátalara? QSpeakerLoc?
    # Kveiktu á útvarpinu inni í eldhúsi
    | QSpeakerKveikja/nhbh "á"? QSpeakerÚtvarp_þgf QSpeakerÍHátalara? QSpeakerLoc?

QSpeakerIncreaseVolume →
    # "Gerðu tónlistina í hátölurunum hærri í stofunni"
    QSpeakerGera/nhbh QSpeakerMusicOrSoundPhrase_þf QSpeakerMeiriOrHærri_nf QSpeakerLoc
    # "Gerðu lætin meiri í stofunni"
    | QSpeakerGera/nhbh QSpeakerMusicOrSoundPhrase_þf QSpeakerLoc? QSpeakerMeiriOrHærri_nf
    # "Gerðu tónlistina meiri í hátölurunum í stofunni"
    | QSpeakerGera/nhbh QSpeakerTónlist_þf QSpeakerMeiriOrHærri_nf QSpeakerÍHátalara QSpeakerLoc?
    # "Gerðu hljóðið hærra í útvarpinu"
    | QSpeakerGera/nhbh QSpeakerHljóðOrLæti_þf QSpeakerMeiriOrHærri_nf QSpeakerÍHátalaraOrÍÚtvarpi? QSpeakerLoc?
    # "Hækkaðu tónlistina í hátölurunum í stofunni"
    | QSpeakerHækka/nhbh QSpeakerMusicOrSoundPhrase_þf? QSpeakerÍHátalara? QSpeakerLoc?
    # "Hækkaðu í hljóðinu í stofunni"
    | QSpeakerHækka/nhbh "í"? QSpeakerMusicOrSoundPhrase_þgf QSpeakerÍHátalara? QSpeakerLoc?
    # "Hækkaðu í útvarpinu"
    | QSpeakerHækka/nhbh "í"? QSpeakerÚtvarp_þgf QSpeakerÍHátalara? QSpeakerLoc?

QSpeakerDecreaseVolume →
    # "Gerðu tónlistina í hátölurunum lægri í stofunni"
    QSpeakerGera/nhbh QSpeakerMusicOrSoundPhrase_þf QSpeakerMinniOrLægri_nf QSpeakerLoc
    # "Gerðu lætin minni í stofunni"
    | QSpeakerGera/nhbh QSpeakerMusicOrSoundPhrase_þf QSpeakerLoc? QSpeakerMinniOrLægri_nf
    # "Gerðu tónlistina minni í hátölurunum í stofunni"
    | QSpeakerGera/nhbh QSpeakerTónlist_þf QSpeakerMinniOrLægri_nf QSpeakerÍHátalara? QSpeakerLoc?
    # "Gerðu hljóðið lægra í útvarpinu"
    | QSpeakerGera/nhbh QSpeakerHljóðOrLæti_þf QSpeakerMinniOrLægri_nf QSpeakerÍHátalaraOrÍÚtvarpi? QSpeakerLoc?
    # "Lækkaðu tónlistina í hátölurunum í stofunni"
    | QSpeakerLækka/nhbh QSpeakerMusicOrSoundPhrase_þf? QSpeakerÍHátalara? QSpeakerLoc?
    # "Lækkaðu í hljóðinu í stofunni"
    | QSpeakerLækka/nhbh "í"? QSpeakerMusicOrSoundPhrase_þgf QSpeakerÍHátalara? QSpeakerLoc?
    # "Lækkaðu í útvarpinu"
    | QSpeakerLækka/nhbh "í"? QSpeakerÚtvarp_þgf QSpeakerÍHátalara? QSpeakerLoc?

# These are not used. They are a part of the older version of this grammar.
# QSpeakerMakeRest →
#     QSpeakerMusicOrSoundPhrase_þf QSpeakerComparative_nf QSpeakerLoc
#     | QSpeakerMusicOrSoundPhrase_þf QSpeakerLoc? QSpeakerComparative_nf
#     | QSpeakerTónlist_þf QSpeakerComparative_nf QSpeakerÍHátalara? QSpeakerLoc?
#     | QSpeakerHljóðOrLæti_þf QSpeakerComparative_nf QSpeakerÍHátalaraOrÍÚtvarpi? QSpeakerLoc?

# QSpeakerSetRest →
#     QSpeakerHátalariOrÚtvarp_þf? QSpeakerÁHvað QSpeakerLoc
#     | QSpeakerHátalariOrÚtvarp_þf? QSpeakerLoc? QSpeakerÁHvað

# QSpeakerLetRest →
#     QSpeakerVeraVerða QSpeakerTónlist_nf QSpeakerÍHátalara? QSpeakerLoc?
#     | QSpeakerMusicOrSoundPhrase_þf QSpeakerLoc QSpeakerVeraVerða QSpeakerComparative_nf
#     | QSpeakerMusicOrSoundPhrase_þf QSpeakerVeraVerða QSpeakerComparative_nf QSpeakerLoc?
#     | QSpeakerTónlist_þf QSpeakerVeraVerða QSpeakerComparative_nf QSpeakerÍHátalara? QSpeakerLoc?
#     | QSpeakerHljóðOrLæti_þf QSpeakerVeraVerða QSpeakerComparative_nf QSpeakerÍHátalaraOrÍÚtvarpi? QSpeakerLoc?

# QSpeakerPlayOrPauseRest →
#     QSpeakerTónlist_þf QSpeakerÍHátalara? QSpeakerLoc?
#     | "útvarpið" QSpeakerÍHátalara? QSpeakerLoc?
#     | "afspilun" QSpeakerÍHátalara? QSpeakerLoc?
#     | QSpeakerÚtvarpsstöðX_þf QSpeakerÍHátalara? QSpeakerLoc?
#     | QSpeakerNextOrPrev_þf QSpeakerÍHátalara? QSpeakerLoc?

# QSpeakerIncreaseOrDecreaseRest →
#     QSpeakerMusicOrSoundPhrase_þf QSpeakerLoc?
#     | "í" QSpeakerTónlistOrHátalariOrÚtvarp_þgf QSpeakerLoc?
#     | "í" QSpeakerRadioStation QSpeakerLoc?

# QSpeakerComparative/fall →
#     QSpeakerMeiriOrHærri/fall
#     | QSpeakerMinniOrLægri/fall

# QSpeakerÁHvað →
#     "yfir"? "á"? QSpeakerTónlistOrÚtvarp_þf
#     | "yfir"? "á"? QSpeakerNewSetting_þf

# QSpeakerNewSetting/fall →
#     QSpeakerÚtvarpsstöðX/fall
#     | QSpeakerNewPlayOrPause/fall
#     | QSpeakerNextOrPrev/fall

# QSpeakerNewPlayOrPause/fall →
#     QSpeakerNewPlay/fall
#     | QSpeakerNewPause/fall

# QSpeakerNewPlay/fall →
#     "play"
#     | "plei"
#     | "pley"

# QSpeakerNewPause/fall →
#     'pása'/fall
#     | 'stopp'/fall

# QSpeakerNextOrPrev/fall →
#     QSpeakerNextSong/fall
#     | QSpeakerPrevSong/fall

# QSpeakerNextSong/fall →
#     'næstur:lo'_kvk/fall 'lag:hk'_et/fall
#     | 'lag:hk'_et_gr/fall "á" "eftir" "þessu"?

# QSpeakerPrevSong/fall →
#     'seinastur:lo'_kvk/fall 'lag:hk'_et/fall
#     | 'síðastur:lo'_kvk/fall 'lag:hk'_et/fall
#     | 'fyrri:lo'_kvk/fall 'lag:hk'_et/fall
#     | 'lag:hk'_et_gr/fall "á" "undan" "þessu"?

QSpeakerÚtvarpsstöðX/fall →
    QSpeakerÚtvarpsstöð/fall? QSpeakerRadioStation

QSpeakerMusicOrSoundPhrase/fall →
    QSpeakerMusicPhrase/fall
    | QSpeakerSoundPhrase/fall

QSpeakerMusicPhrase/fall →
    QSpeakerTónlist/fall QSpeakerHátalariOrÚtvarp_ef?
    | QSpeakerTónlist/fall "í" QSpeakerHátalariOrÚtvarp_þgf

QSpeakerSoundPhrase/fall →
    QSpeakerHljóðOrLæti/fall? QSpeakerTónlistOrHátalariOrÚtvarp_ef?
    | QSpeakerHljóðOrLæti/fall "í" QSpeakerTónlistOrHátalariOrÚtvarp_þgf?

QSpeakerHljóðOrLæti/fall →
    QSpeakerHljóð/fall
    | QSpeakerLæti/fall

QSpeakerTónlistOrHátalariOrÚtvarp/fall →
    QSpeakerTónlist/fall
    | QSpeakerHátalariOrÚtvarp/fall

QSpeakerÚtvarpsstöð/fall →
    'útvarpsstöð'_et/fall
    | 'útvarp:hk'? 'stöð:kvk'_et/fall

QSpeakerTónlist/fall → 'tónlist:kvk'_et/fall | 'tónlistarmaður:kk' # TODO: Fix in beautified query

QSpeakerHljóð/fall →
    'hljóð'_et/fall
    | 'hljóðstyrkur'_et/fall
    | 'ljóð'_et/fall

QSpeakerLæti/fall →
    'læti'_ft/fall
    | 'hávaði'_et/fall

QSpeakerHátalari/fall → 'hátalari'/fall
QSpeakerÚtvarp/fall → 'útvarp'/fall

QSpeakerHátalariOrÚtvarp/fall →
    QSpeakerHátalari/fall
    | QSpeakerÚtvarp/fall

QSpeakerÍHátalaraOrÍÚtvarpi → "í" QSpeakerHátalariOrÚtvarp_þgf
QSpeakerÍHátalara → "í" QSpeakerHátalari_þgf
# TODO: ^ Score needed for these NT's?

QSpeakerTónlistOrÚtvarp/fall →
    QSpeakerTónlist/fall
    | QSpeakerÚtvarp/fall

QSpeakerMeiriOrHærri/fall →
    'mikill:lo'_mst/fall
    | 'hár:lo'_mst/fall

QSpeakerMinniOrLægri/fall →
    'lítill:lo'_mst/fall
    | 'lágur:lo'_mst/fall

QSpeakerStaðarAo →
    "fram"
    | "frammi"
    | "inn"
    | "inni"
    | "niðri"
    | "niður"
    | "upp"
    | "uppi"
    | "út"
    | "úti"

QSpeakerÁOrÍ → "á" | "í"

QSpeakerRoom/fall → no/fall

QSpeakerAlltHús/fall → 'allur:fn'_et_hk/fall? 'hús:hk'_et/fall
QSpeakerEverywhere →
    "alls_staðar"
    | "alstaðar"
    | "allstaðar"
    | "allsstaðar"
    | "alsstaðar"
    | "á" "öllum" 'staður:kk'_ft_þgf
    | "á" "öllum" 'staðsetning:kvk'_ft_þgf
    | "í" "öllum" 'herbergi:hk'_ft_þgf
    | "út" "um" QSpeakerAlltHús_nf
    | "í" QSpeakerAlltHús_þgf

QSpeakerLoc →
    # "inni í eldhúsi", "úti á palli", "frammi í stofu", ...
    QSpeakerStaðarAo? QSpeakerÁOrÍ QSpeakerRoom_þgf
    # "alls staðar", "í öllu húsinu", "í öllum herbergjum", ...
    | QSpeakerEverywhere

# List of the supported radio stations
QSpeakerRadioStation →
    QSpeakerApparatið
    | QSpeakerBylgjan
    | QSpeakerFlashback
    | QSpeakerFM957
    | QSpeakerFMExtra
    | QSpeakerGullbylgjan
    | QSpeakerK100
    | QSpeakerKissFM
    | QSpeakerLéttbylgjan
    | QSpeakerRetro
    | QSpeakerRondó
    | QSpeakerRás1
    | QSpeakerRás2
    | QSpeakerX977
    | QSpeakerÍslenskaBylgjan
    | QSpeakerÚtvarp101
    | QSpeakerÚtvarpSuðurland
    | QSpeakerÚtvarpSaga
    | QSpeaker70sFlashback
    | QSpeaker80sFlashback
    | QSpeaker80sBylgjan
    | QSpeaker90sFlashback

QSpeakerBylgjan →
    "Bylgjan"
    | "Bylgjuna"
    | "Bylgjunni"
    | "Bylgjunnar"
    | "bylgjan"
    | "bylgjuna"
    | "bylgjunni"
    | "bylgjunnar"

QSpeakerÚtvarpSaga →
    "Útvarp" "Saga"
    | "Útvarp" "Sögu"
    | "Útvarpi" "Sögu"
    | "Útvarp" "Sögu"
    | "Útvarps" "Sögu"
    | "útvarp" "saga"
    | "útvarp" "sögu"
    | "útvarpi" "sögu"
    | "útvarp" "sögu"
    | "útvarps" "sögu"
    | "útvarpssaga"
    | "útvarpssögu"

QSpeakerGullbylgjan →
    "Gullbylgjan"
    | "Gullbylgjuna"
    | "Gullbylgjunni"
    | "Gullbylgjunnar"
    | "gullbylgjan"
    | "gullbylgjuna"
    | "gullbylgjunni"
    | "gullbylgjunnar"
    | "gull" "bylgjan"
    | "gull" "bylgjuna"
    | "gull" "bylgjunni"
    | "gull" "bylgjunnar"

QSpeakerLéttbylgjan →
    "Léttbylgjan"
    | "Léttbylgjuna"
    | "Léttbylgjunni"
    | "Léttbylgjunnar"
    | "léttbylgjan"
    | "léttbylgjuna"
    | "léttbylgjunni"
    | "léttbylgjunnar"
    | "létt" "bylgjan"
    | "létt" "bylgjuna"
    | "létt" "bylgjunni"
    | "létt" "bylgjunnar"

QSpeakerX977 →
    "X-ið" "977"?
    | "X-inu" "977"?
    | "X-ins" "977"?
    | "x-ið" "977"?
    | "x-inu" "977"?
    | "x-ins" "977"?
    | "x" "977"
    | "x977"
    | "x" "níu" "sjö" "sjö"
    | "x-977"

QSpeakerRás1 →
    "rás" "1"
    | "rás" "eitt"

QSpeakerRás2 →
    "rás" "2"
    | "rás" "tvö"

QSpeakerRondó →
    "rondo" "fm"?
    | "rondó" "fm"?
    | "róndó" "fm"?
    | "London"

QSpeakerFM957 →
    "fm" "957"
    | "fm957"

QSpeakerK100 →
    "k" "100"
    | "k" "hundrað"
    | "k100"
    | "k-hundrað"
    | "k-100"
    | "kk" "hundrað"
    | "kk" "100"

QSpeakerRetro →
    "retro" "fm"?
    | "retró" "fm"?

QSpeakerKissFM →
    "kiss" "fm"?

QSpeakerFlashback →
    "flassbakk" "fm"?
    | "flass" "bakk" "fm"?

QSpeakerÚtvarp101 →
    "útvarp"? "101"
    | "útvarp"? "hundrað" "og" "einn"
    | "útvarp"? "hundrað" "og" "eitt"
    | "útvarp"? "hundrað" "einn"
    | "útvarp"? "hundrað" "1"

QSpeaker80sBylgjan →
    QSpeaker80s "Bylgjan"
    | QSpeaker80s "Bylgjuna"
    | QSpeaker80s "Bylgjunni"
    | QSpeaker80s "Bylgjunnar"
    | QSpeaker80s "bylgjan"
    | QSpeaker80s "bylgjuna"
    | QSpeaker80s "bylgjunni"
    | QSpeaker80s "bylgjunnar"

QSpeakerÍslenskaBylgjan →
    "íslenska" "Bylgjan"
    | "íslensku" "Bylgjuna"
    | "íslensku" "Bylgjunni"
    | "íslensku" "Bylgjunnar"
    | "íslenska" "bylgjan"
    | "íslensku" "bylgjuna"
    | "íslensku" "bylgjunni"
    | "íslensku" "bylgjunnar"

QSpeakerApparatið → "apparatið"

QSpeakerFMExtra → "fm" "extra"

QSpeakerÚtvarpSuðurland →
    "útvarp"? "suðurland"
    | "útvarps"? "suðurlands"

QSpeaker70sFlashback → QSpeaker70s QSpeakerFlashbackWord
QSpeaker80sFlashback → QSpeaker80s QSpeakerFlashbackWord
QSpeaker90sFlashback → QSpeaker90s QSpeakerFlashbackWord

QSpeakerFlashbackWord →
    "flassbakk"
    | "flass" "bakk"
    | "bakk"
    | "flaska"
    | "flashback"
    | "flash" "back"

QSpeaker70s →
    "seventís"
    | "seventies"
    | "70"
    | "70s"

QSpeaker80s →
    "eitís"
    | "Eydís"
    | "eydís"
    | "Eidís"
    | "eidís"
    | "eighties"
    | "80"
    | "80s"

QSpeaker90s →
    "næntís"
    | "nineties"
    | "90"
    | "90s"
